apply plugin: 'maven-publish'

afterEvaluate { project ->
    def pomConfig = {
        scm {
            url 'https://github.com/orchid-revival/Orchid'
            connection 'https://github.com/orchid-revival/Orchid.git'
            developerConnection 'https://github.com/orchid-revival/Orchid'
        }
        licenses {
            license {
                name 'LGPL-3.0'
                url 'https://opensource.org/licenses/LGPL-3.0'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'SnakeSVx'
                name 'Stijn Vlaes'
                email 'stijn.sv@gmail.com'
            }
        }
    }

    project.publishing {
        publications {
            OrchidPublication(MavenPublication) {
                from project.components.java
                groupId "${project.group}"
                artifactId "${project.name}"
                version "${project.version}"
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'A modern, extensible, theme-ready Javadoc site generator')
                    root.appendNode('name', 'Orchid')
                    root.appendNode('url', 'https://github.com/orchid-revival/Orchid')
                    root.children().last() + pomConfig
                }
            }
        }
    }

}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}
